{ pkgs, fetchFromGitHub,  ... }:
let
  resurrectDirPath = "~/.config/tmux/resurrect";

  mkTmuxPlugin = pkgs.tmuxPlugins.mkTmuxPlugin;
  sessionx = mkTmuxPlugin {
    pluginName = "sessionx";
    version = "unstable-2024-08-16";
    src = pkgs.fetchFromGitHub {
      owner = "omerxx";
      repo = "tmux-sessionx";
      rev = "ecc926e7db7761bfbd798cd8f10043e4fb1b83ba";
      sha256 = "1qa2a4m75w6k64f52fsw9k6yyiidlxm2q31w8hrsjd5bcdr6dzab";
    };
  };
in
{
  home.packages = with pkgs; [ fzf ]; #dependency needed by sessionx
  programs.tmux = {
    enable = true;
    prefix = "C-Space";
    mouse = true;
    terminal = "tmux-256color";
    historyLimit = 100000;
    keyMode = "vi";
    baseIndex = 1;
    clock24 = true;
    sensibleOnTop = true;
    
    plugins = with pkgs;[
      tmuxPlugins.better-mouse-mode
      {
        plugin = tmuxPlugins.catppuccin;
        extraConfig = ''
          set -g @catppuccin_flavor 'frappe'
          set -g @catppuccin_window_tabs_enabled on
          set -g @catppuccin_date_time "%H:%M"
        '';
      }
      {
        plugin = tmuxPlugins.resurrect;
        extraConfig = ''
          set -g @resurrect-strategy-vim 'session'
          set -g @resurrect-strategy-nvim 'session'
          set -g @resurrect-capture-pane-contents 'on'
          
          # This three lines are specific to NixOS and they are intended
          # to edit the tmux_resurrect_* files that are created when tmux
          # session is saved using the tmux-resurrect plugin. Without going
          # into too much details the strings that are saved for some applications
          # such as nvim, vim, man... when using NixOS, appimage, asdf-vm into the
          # tmux_resurrect_* files can't be parsed and restored. This addition
          # makes sure to fix the tmux_resurrect_* files so they can be parsed by
          # the tmux-resurrect plugin and successfully restored.
          set -g @resurrect-dir ${resurrectDirPath}
          set -g @resurrect-hook-post-save-all 'sed -i -E "s|(pane.*nvim\s*:)[^;]+;.*\s([^ ]+)$|\1nvim \2|" ${resurrectDirPath}/last'
          '';
      }
      {
        plugin = tmuxPlugins.vim-tmux-navigator;
      }
      {
        plugin = tmuxPlugins.continuum;
        extraConfig = ''
          set -g @continuum-restore 'on'
          set -g @continuum-boot 'on'
          set -g @continuum-save-interval '10'
        '';
      }
      # tmuxPlugins.tmux-fzf
      sessionx
    ];
    # extraConfig = '' # automaticaly generated by the plugin not needed
    # run-shell ${sessionx}/share/tmux-plugins/sessionx/sessionx.tmux
    # '';
  };
}
